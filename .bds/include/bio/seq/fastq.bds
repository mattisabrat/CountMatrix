#!/usr/bin/env bds

#-------------------------------------------------------------------------------
# Basic FASTQ manipulation
#
#															Pablo Cingolani 2014
#-------------------------------------------------------------------------------

#-------------------------------------------------------------------------------
# Run task  to split a fastq file into 'splitNumReads' reads
#-------------------------------------------------------------------------------
string splitFatsq(string fq, int splitNumReads) {
	# Empty name? Nothing to do
	if( ! fq )	return("")

	# Compressed files use gunzip
	cat := "cat"
	if( fq.endsWith('.gz') )	cat = "gunzip -c"	

	names := removeExtFastqa( fq ) + "."
	numLines := 4 * splitNumReads
	firstName := names + "aa"
	print("Splitting file '$fq' into $numLines lines: $names\n")
	task( firstName <- fq, cpus := 1 ) sys $cat $fq | split -l $numLines - $names
	return( names )
}

#-------------------------------------------------------------------------------
# Get names form split fastq files
#-------------------------------------------------------------------------------
string[] splitFatsqNames(string fq) {
	string[] empty

	# Empty name? Nothing to do
	if( ! fq )	return(empty)

	names := removeExtFastqa( fq ) + "."
	dir := fq.dirName()
	base := removeExtFastqa( fq ).baseName() + "."

	# Get all lines matching base name in fq's directory
	return( dir.dirPath(".*/$base..") )
}

#-------------------------------------------------------------------------------
# Base name, remove all common FASTA/FASTQ extensions
#-------------------------------------------------------------------------------
string removeExtFastqa(string name) {
	for( string ext : [".gz", ".fq", ".fastq", ".fa", ".fasta"] ) name = name.removeExt(ext)
	return( name )
}

