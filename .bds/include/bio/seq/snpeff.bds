#!/usr/bin/env bds

#-------------------------------------------------------------------------------
#
# SnpEff related variables
#
#-------------------------------------------------------------------------------

# Default directory
snpEffDir := "$HOME/snpEff"		help Path to SnpEff's dir

# Comands
string snpEff="$snpEffJava -Xmx4g -jar $snpEffDir/snpEff.jar"
string snpSift="$snpEffJava -Xmx2g -jar $snpEffDir/SnpSift.jar"

# Gene set analysis correction programs
string correctionPvalMin   = "$snpEffDir/scripts/gsa/pvalue_correction_scoreCount.sh"
string correctionPvalMin10 = "$snpEffDir/scripts/gsa/pvalue_correction_scoreCount_min10.sh"
string correctionBfMax     = "$snpEffDir/scripts/gsa/bayesFactor_correction_scoreCount.sh"
string correctionBfMax10   = "$snpEffDir/scripts/gsa/bayesFactor_correction_scoreCount_max10.sh"

#-------------------------------------------------------------------------------
# Annotate all VCF files in a dir
#-------------------------------------------------------------------------------

string annotateAllVcf(string vcfDir) {

	# Find all vcf files that have not been annotated yet
	string findId = sys find $vcfDir -iname "*.vcf" | grep -v "eff.vcf" 
	string[] vcfsAll = findId.stdout().lines()

	# Create a list of VCF file that have to be annotates (i.e. there is no corresponding "*.eff.vcf" file)
	print("Annotate files in directory '$vcfDir'\n")
	string[] vcfs
	for( string vcf : vcfsAll ) {
		string eff = vcf.dirName() + "/" + vcf.baseName(".vcf") + ".eff.vcf"

		# Do we need to annotate this file? Add it to the lists
		if( eff <- vcf ) {
			vcfs.add(vcf)
			print("\tNot found: $eff\n")
		}
	}

	# Anything to annotate?
	if( ! vcfs.isEmpty() ) {
		# Create a file with all VCF files to annotate
		string filesToAnnotate = "$vcfDir/vcf_files.txt"
		filesToAnnotate.write( vcfs.join("\n") )

		# Run task
		task $snpEff eff -v -c $snpEffDir/snpEff.config -noStats -fileList -lof GRCh37.71 $filesToAnnotate
	}

	return (vcfs)
}

